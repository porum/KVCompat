plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'io.github.porum.kvcompat'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "com.tencent:mmkv-static:1.2.15"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {

    task sourceJar(type: Jar) {
        from android.sourceSets.main.java.sourceFiles
        archiveClassifier.set("sources")
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
    }

    publishing {
        publications {
            release(MavenPublication) {

                groupId = GROUP
                version = VERSION

                from components.release
                artifact sourceJar
                artifact javadocJar

                pom {
                    name = "KVCompat"
                    description = "KVCompat"
                    url = "https://github.com/porum/KVCompat"
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'porum'
                            name = 'guobao.sun'
                            email = 'sunguobao12@gmail.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/porum/KVCompat'
                    }
                }
            }
        }

        repositories {
            maven {
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = sonatypeUserName
                    password = sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}